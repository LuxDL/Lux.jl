# Wrappers over Base & LinearAlgebra implementations to use poly algs if needed
matmuladd(A, B, ::Nothing) = matmul(A, B)
function matmuladd(A::AbstractMatrix, B::AbstractVector, bias::AbstractVector)
    return matmuladd(A, reshape(B, :, 1), bias)
end
function matmuladd(A::AbstractMatrix, B::AbstractMatrix, bias::AbstractVector)
    return matmuladd(internal_operation_mode((A, B, bias)), A, B, bias)
end

function matmuladd(
        ::GenericBroadcastOp, A::AbstractMatrix, B::AbstractMatrix, bias::AbstractVector)
    return muladd(A, B, bias)
end
function matmuladd(opmode::AbstractInternalArrayOpMode, A::AbstractMatrix,
        B::AbstractMatrix, bias::AbstractVector)
    if size(A, 2) != size(B, 1)
        throw(DimensionMismatch(lazy"A has shape ($(size(A, 1)), $(size(A, 2))) but B has shape ($(size(B, 1)), $(size(B, 2)))"))
    end
    if length(bias) != size(A, 1)
        throw(DimensionMismatch(lazy"bias has length $(length(bias)) but A has shape ($(size(A, 1)), $(size(A, 2)))"))
    end
    C = similar(A, promote_type(eltype(A), eltype(B), eltype(bias)), size(A, 1), size(B, 2))
    matmuladd!(C, opmode, A, B, bias)
    return C
end

matmul(A::AbstractMatrix, B::AbstractVector) = vec(matmul(A, reshape(B, :, 1)))
function matmul(A::AbstractMatrix, B::AbstractMatrix)
    if size(A, 2) != size(B, 1)
        throw(DimensionMismatch(lazy"A has shape ($(size(A, 1)), $(size(A, 2))) but B has shape ($(size(B, 1)), $(size(B, 2)))"))
    end
    return matmul(internal_operation_mode((A, B)), A, B)
end

matmul(::GenericBroadcastOp, A::AbstractMatrix, B::AbstractMatrix) = A * B
function matmul(::AbstractInternalArrayOpMode, A::AbstractMatrix, B::AbstractMatrix)
    C = similar(A, promote_type(eltype(A), eltype(B)), size(A, 1), size(B, 2))
    matmul!(C, A, B)
    return C
end

# Slightly higher level. Here we make decisions about which implementation to use
function matmuladd!(C::AbstractMatrix, A::AbstractMatrix, B::AbstractMatrix, ::Nothing)
    matmul!(C, A, B)
    return
end
function matmuladd!(
        C::AbstractMatrix, A::AbstractMatrix, B::AbstractMatrix, bias::AbstractVector)
    matmuladd!(C, internal_operation_mode((C, A, B, bias)), A, B, bias)
    return
end

function matmuladd!(C::AbstractMatrix, ::AbstractInternalArrayOpMode,
        A::AbstractMatrix, B::AbstractMatrix, bias::AbstractVector)
    matmuladd_generic!(C, A, B, bias)
    return
end

function matmuladd!(C::AbstractMatrix, ::GPUBroadcastOp{CUDADevice},
        A::AbstractMatrix, B::AbstractMatrix, bias::AbstractVector)
    cublasLt_fused_dense!(C, identity, A, B, bias)
    return
end

function matmuladd!(C::AbstractMatrix, ::LoopedArrayOp, A::AbstractMatrix,
        B::AbstractMatrix, bias::AbstractVector)
    matmuladd_cpu!(C, System.use_octavian(), A, B, bias)
    return
end

function matmuladd_cpu!(C::AbstractMatrix, ::False, A::AbstractMatrix,
        B::AbstractMatrix, bias::AbstractVector)
    if LV.check_args(C, A, B) && System.fits_in_l1cache(C, A, B)
        matmuladd_loopvec!(C, A, B, bias)
        return
    end
    matmuladd_generic!(C, A, B, bias)
    return
end

function matmuladd_cpu!(C::AbstractMatrix, ::True, A::AbstractMatrix,
        B::AbstractMatrix, bias::AbstractVector)
    if LV.check_args(C, A, B)
        if System.fits_in_l1cache(C, A, B)
            matmuladd_loopvec!(C, A, B, bias)
            return
        elseif System.fits_in_l3cache(C, A, B)
            matmuladd_octavian!(C, A, B, bias)
            return
        end
    end
    matmuladd!(C, GenericBroadcastOp(), A, B, bias)
    return
end

function matmul!(C::AbstractMatrix, A::AbstractMatrix, B::AbstractMatrix)
    matmul!(C, internal_operation_mode((C, A, B)), A, B)
    return
end

function matmul!(C::AbstractMatrix, ::AbstractInternalArrayOpMode,
        A::AbstractMatrix, B::AbstractMatrix)
    matmul_generic!(C, A, B, true, false)
    return
end

function matmul!(C::AbstractMatrix, ::LoopedArrayOp, A::AbstractMatrix, B::AbstractMatrix)
    return matmul_cpu!(C, System.use_octavian(), A, B)
end

function matmul_cpu!(C::AbstractMatrix, ::True, A::AbstractMatrix, B::AbstractMatrix)
    dims = (size(C, 1), size(A, 2), size(B, 2))
    if LV.check_args(C, A, B)
        if System.fits_in_l1cache(C, A, B)
            serial_matmul_loopvec!(C, A, B, true, false)
            return
        elseif System.fits_in_l3cache(C, A, B)
            matmul_octavian!(C, A, B, true, false)
            return
        end
    end
    matmul_generic!(C, A, B, true, false)
    return
end

function matmul_cpu!(C::AbstractMatrix, ::False, A::AbstractMatrix, B::AbstractMatrix)
    if LV.check_args(C, A, B) && System.fits_in_l1cache(C, A, B)
        matmul_loopvec!(C, A, B, true, false)
        return
    end
    matmul_generic!(C, A, B, true, false)
    return
end

# Low-Level Matmul implementations -- Either call libraries or implement our own
function matmul_octavian!(
        C::AbstractMatrix, A::AbstractMatrix, B::AbstractMatrix, Î±::Number, Î²::Number)
    Octavian.matmul!(C, A, B, Î±, Î²)
    return
end

function matmul_generic!(
        C::AbstractMatrix, A::AbstractMatrix, B::AbstractMatrix, Î±::Number, Î²::Number)
    mul!(C, A, B, Î±, Î²)
    return
end

for serial in (true, false)
    opname = serial ? :serial_matmul_loopvec! : :matmul_loopvec!
    @eval function $opname(
            C::AbstractMatrix, A::AbstractMatrix, B::AbstractMatrix, Î±::Number, Î²::Number)
        if !iszero(Î²) # Secial case this because Base.FastMath.mul_fast(NaN, false) = NaN
            @turbo thread=$(!serial) for K in indices((C, B), 2), J in indices((C, A), 1)
                Câ±¼â‚– = zero(eltype(C))
                for I in indices((A, B), (2, 1))
                    Câ±¼â‚– += A[J, I] * B[I, K]
                end
                C[J, K] = Î± * Câ±¼â‚– + Î² * C[J, K]
            end
        else
            @turbo thread=$(!serial) for K in indices((C, B), 2), J in indices((C, A), 1)
                Câ±¼â‚– = zero(eltype(C))
                for I in indices((A, B), (2, 1))
                    Câ±¼â‚– += A[J, I] * B[I, K]
                end
                C[J, K] = Î± * Câ±¼â‚–
            end
        end
    end
end

function matmuladd_loopvec!(
        C::AbstractMatrix, A::AbstractMatrix, B::AbstractMatrix, bias::AbstractVector)
    @tturbo for K in indices((C, B), 2), J in indices((C, A), 1)
        Câ±¼â‚– = zero(eltype(C))
        for I in indices((A, B), (2, 1))
            Câ±¼â‚– += A[J, I] * B[I, K]
        end
        C[J, K] = bias[J] + Câ±¼â‚–
    end
    return
end

function matmuladd_generic!(
        C::AbstractMatrix, A::AbstractMatrix, B::AbstractMatrix, bias::AbstractVector)
    C .= bias
    matmul_generic!(C, A, B, true, true)
    return
end

function matmuladd_octavian!(
        C::AbstractMatrix, A::AbstractMatrix, B::AbstractMatrix, bias::AbstractVector)
    matmul_octavian!(C, A, B, true, false)
    bias_add!(C, internal_operation_mode((C, bias)), C, bias)
    return
end

# ChainRules
function CRC.rrule(::typeof(matmul), A::AbstractMatrix, B::AbstractMatrix)
    ğ’«A, ğ’«B = CRC.ProjectTo(A), CRC.ProjectTo(B)
    âˆ‡matmul = @closure Î”â€² -> begin
        Î” = CRC.unthunk(Î”â€²)
        âˆ‚A = CRC.@thunk(ğ’«A(matmul(Î”, B')))
        âˆ‚B = CRC.@thunk(ğ’«B(matmul(A', Î”)))
        return âˆ‚âˆ…, âˆ‚A, âˆ‚B
    end
    return matmul(A, B), âˆ‡matmul
end

function CRC.rrule(
        ::typeof(matmuladd), A::AbstractMatrix, B::AbstractMatrix, bias::AbstractVector)
    ğ’«A, ğ’«B, ğ’«bias = CRC.ProjectTo(A), CRC.ProjectTo(B), CRC.ProjectTo(bias)
    âˆ‡matmuladd = @closure Î”â€² -> begin
        Î” = CRC.unthunk(Î”â€²)
        âˆ‚A = CRC.@thunk(ğ’«A(matmul(Î”, B')))
        âˆ‚B = CRC.@thunk(ğ’«B(matmul(A', Î”)))
        âˆ‚bias = CRC.@thunk(ğ’«bias(âˆ‡bias_add(bias, Î”)))
        return âˆ‚âˆ…, âˆ‚A, âˆ‚B, âˆ‚bias
    end
    return matmuladd(A, B, bias), âˆ‡matmuladd
end

# EnzymeRules
Utils.@enzyme_reverse_alternative matmul_octavian! matmul_generic!
Utils.@enzyme_reverse_alternative serial_matmul_loopvec! matmul_generic!
Utils.@enzyme_reverse_alternative matmul_loopvec! matmul_generic!

Utils.@enzyme_reverse_alternative matmuladd_octavian! matmuladd_generic!
Utils.@enzyme_reverse_alternative matmuladd_loopvec! matmuladd_generic!
